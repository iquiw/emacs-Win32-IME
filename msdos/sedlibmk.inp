# -sedlibmk.inp-----------------------------------------------------------
# Configuration script for lib/Makefile under DJGPP v2.x
# ----------------------------------------------------------------------
#
# Copyright (C) 2011-2014 Free Software Foundation, Inc.
#
# This file is part of GNU Emacs.
#
# This file is free software; as a special exception, the author gives
# unlimited permission to copy and/or distribute it, with or without
# modifications, as long as this notice is preserved.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
#
# ----------------------------------------------------------------------
#
# Checklist to add a new gnulib module:
#
#  . If the module includes source files that need to be compiled, and
#    does not appear in the am_libgnu_a_OBJECTS list, add the
#    corresponding .o file names to the list that gets assigned to the
#    gl_LIBOBJS variable.
#
#  . If a module appears in am_libgnu_a_OBJECTS that is not required
#    for the MSDOS build, edit it out by adding an appropriate command
#    to the set that edits the am_libgnu_a_OBJECTS block.
#
#  . If the module defines functions that need to replace DJGPP
#    functions, edit the appropriate REPLACE_foo variables to 1:
#
#     /^REPLACE_CALLOC *=/s/@REPLACE_CALLOC@/1/
#
#    Note that this file already has a command to edit all the
#    REPLACE_foo variables to zero, so if some gnulib replacement
#    function is ever needed, the corresponding editing command such
#    as above should be placed before that catchall rule (search for
#    "REPLACE_" below).
#
#  . If the module is a header or adds headers, edit the corresponding
#    variable to either an empty value or to the name of the header.
#    Examples:
#
#       /^STDINT_H *=/s/@[^@\n]*@/stdint.h/  -- stdint.h is needed
#       /^STDDEF_H *=/s/@[^@\n]*@//          -- stddef.h is not needed
#
#  . Some of the headers are generated conditionally, and the
#    corresponding recipes are guarded by @GL_GENERATE_xxxx_H_TRUE@
#    and @GL_GENERATE_xxxx_H_FALSE@.  Depending on whether DJGPP uses
#    the corresponding header, these should be edited either to
#    nothing (thus exposing the recipe) or to #, which comments the
#    recipe away.  Example:
#
#       s/^@GL_GENERATE_STDBOOL_H_TRUE@/\#/
#       s/^@GL_GENERATE_STDBOOL_H_FALSE@//
#
#  . Also edit the NEXT_foo_H and NEXT_AS_FIRST_DIRECTIVE_foo_H
#    variables as appropriately: to an empty value if the gnulib
#    header is not used, and to the corresponding DJGPP header name
#    otherwise.  Examples:
#
#     /^NEXT_STDDEF_H *=/s/@[^@\n]*@//
#     /^NEXT_STDINT_H *=/s/@[^@\n]*@/<stdint.h>/
#     /^NEXT_AS_FIRST_DIRECTIVE_GETOPT_H *=/s/@[^@\n]*@/<getopt.h>/
#     /^NEXT_AS_FIRST_DIRECTIVE_STDDEF_H *=/s/@[^@\n]*@//
#
#  . Note that some gnulib headers cannot be left unused: those for
#    which there's no corresponding foo_H variable in
#    msdos/autogen/Makefile.in (example: stdio.h).  For these the "not
#    needed" path is not applicable.
#
#  . If the header is needed, edit all the variables it uses as
#    appropriate.  In general, if DJGPP has the corresponding feature,
#    the value of the corresponding HAVE_foo should be set to what
#    DJGPP has, and the corresponding GNULIB_foo variable should be
#    edited to zero.  If the feature is not available, the HAVE_foo
#    variable should be edited to zero and the corresponding
#    GNULIB_foo variable should be edited to 1 if the gnulib
#    replacement can work for DJGPP (a rare phenomenon), zero
#    otherwise.  Note that by default all HAVE_foo and GNULIB_foo
#    variables that don't have a specific editing rule will be edited
#    to zero.
#
#  . Some gnulib header files have more complex recipes in
#    msdos/autogen/Makefile.in than others.  The tell-tale sign of
#    such a recipe is that the input redirection from the .in.h file
#    is not present at the end of the command, like this:
#
#	      -e '/definition of _GL_WARN_ON_USE/r $(WARN_ON_USE_H)'; \
#	} > $@-t && \
#	mv $@-t $@
#
#    as opposed to this:
#
#	      -e '/definition of _GL_WARN_ON_USE/r $(WARN_ON_USE_H)' \
#	      < $(srcdir)/sys_stat.in.h; \
#	} > $@-t && \
#	mv $@-t $@
#
#    For those recipes that do _not_ have redirection at the end of
#    the command, add a block of Sed commands like this:
#
#     /^stdio\.h:/,/^	[ 	]*mv /{
#       s/'\; \\ *$/' >> $@-t/
#     }
#
#    There's also a 3rd variety of script, which ends like this:
#
#             -e '/definition of _GL_WARN_ON_USE/r $(WARN_ON_USE_H)'; \
#             < $(srcdir)/string.in.h; \
#       } > $@-t && \
#       mv $@-t $@
#
#    For these, you will need Sed commands like those used for
#    string.h, see below.
#
#  . If the recipe for generating a header file uses 'cat', replace it with
#
#      sed -e ''
#
#    (if needed; there's already an edit command that should take care of
#     this).
#
# The following Awk script is useful for editing portions of
# msdos/autogen/Makefile.in into Sed commands that define the
# corresponding variables to zero (which is what is required in the
# absolute majority of cases):
#
#   { printf "/^%s *=/s/%s/0/\n",$1,$3}
#
# ----------------------------------------------------------------------
# Replace @..@ constants.
/^\# @configure_input@/s!@configure_input@!lib/Makefile.  Generated from Makefile.in by config.bat!
/^@SET_MAKE@$/s/@SET_MAKE@//
/^VPATH *=/s/@[^@\n]*@/./
s/@PACKAGE@/emacs/
/^am__cd *=/c\
am__cd = cd
/^@BUILDING_FOR_WINDOWSNT_TRUE@/s/@[^@\n]*@/#/
/^@BUILDING_FOR_WINDOWSNT_FALSE@/s/@[^@\n]*@//
/^ALLOCA *=/s/@[^@\n]*@//
/^ALSA_CFLAGS *=/s/@[^@\n]*@//
/^ALSA_LIBS *=/s/@[^@\n]*@//
/^AR *=/s/@[^@\n]*@/ar/
/^ARFLAGS *=/s/@[^@\n]*@/cru/
/^AWK *=/s/@[^@\n]*@/gawk/
/^CANNOT_DUMP *=/s/@[^@\n]*@/no/
/^CC *=/s/@[^@\n]*@/gcc/
/^CPP *=/s/@[^@\n]*@/gcc -e/
/^CPPFLAGS *=/s/@[^@\n]*@//
/^CCDEPMODE *=/s/@[^@\n]*@/depmode=gcc3/
/^CFLAGS *=/s/@[^@\n]*@/-g -O2/
/^CYGPATH_W *=/s/@[^@\n]*@//
/^CYGWIN_OBJ *=/s/@[^@\n]*@//
/^C_SWITCH_MACHINE *=/s/@C_SWITCH_MACHINE@//
/^C_SWITCH_SYSTEM *=/s/@C_SWITCH_SYSTEM@//
/^GNUSTEP_CFLAGS *=/s/@GNUSTEP_CFLAGS@//
/^C_SWITCH_X_SITE *=/s/@C_SWITCH_X_SITE@//
/^PROFILING_CFLAGS *=/s/@PROFILING_CFLAGS@//
/^GNULIB_WARN_CFLAGS *=/s/@GNULIB_WARN_CFLAGS@//
/^WARN_CFLAGS *=/s/@WARN_CFLAGS@//
/^WERROR_CFLAGS *=/s/@WERROR_CFLAGS@//
/^DEFS *=/s/@[^@\n]*@//
/^DEPDIR *=/s/@[^@\n]*@/deps/
/^DEPFLAGS *=/s/@[^@\n]*@/-MMD -MF ${DEPDIR}\/$*.d/
/^ECHO_N *=/s/@[^@\n]*@/-n/
/^EXEEXT *=/s/@[^@\n]*@/.exe/
/^EXECINFO_H *=/s/@[^@\n]*@/execinfo.h/
/^GETOPT_H *=/s/@[^@\n]*@/getopt.h/
#
# Gnulib stuff
/^BITSIZEOF_PTRDIFF_T *=/s/@BITSIZEOF_PTRDIFF_T@/32/
/^BITSIZEOF_SIG_ATOMIC_T *=/s/@BITSIZEOF_SIG_ATOMIC_T@/32/
/^BITSIZEOF_SIZE_T *=/s/@BITSIZEOF_SIZE_T@/32/
/^BITSIZEOF_WCHAR_T *=/s/@BITSIZEOF_WCHAR_T@/16/
/^BITSIZEOF_WINT_T *=/s/@BITSIZEOF_WINT_T@/32/
/^APPLE_UNIVERSAL_BUILD *=/s/@APPLE_UNIVERSAL_BUILD@/0/
#
# Most GNULIB_* are replaced with zero even though DJGPP does not have
# these features.  That's because the gnulib replacements cannot
# possibly work for DJGPP, so we prefer to fail the link than have a
# subtly botched executable.  Those replacements that _are_ needed
# should be before the last catch-all rule.
/^GNULIB_ATOLL *=/s/@GNULIB_ATOLL@/1/
/^GNULIB_DUP3 *=/s/@GNULIB_DUP3@/1/
/^GNULIB_ENVIRON *=/s/@GNULIB_ENVIRON@/1/
/^GNULIB_FDATASYNC *=/s/@GNULIB_FDATASYNC@/1/
/^GNULIB_GETLOADAVG *=/s/@GNULIB_GETLOADAVG@/1/
/^GNULIB_GL_UNISTD_H_GETOPT *=/s/@GNULIB_GL_UNISTD_H_GETOPT@/1/
/^GNULIB_MEMRCHR *=/s/@GNULIB_MEMRCHR@/1/
/^GNULIB_MKOSTEMP *=/s/@GNULIB_MKOSTEMP@/1/
/^GNULIB_TIME_R *=/s/@GNULIB_TIME_R@/1/
/^GNULIB_UNSETENV *=/s/@GNULIB_UNSETENV@/1/
/^GNULIB_[^ =]* *= *@/s/@[^@\n]*@/0/
/^GSETTINGS_CFLAGS *=/s/@[^@\n]*@//
/^GSETTINGS_LIBS *=/s/@[^@\n]*@//
#
# Edit the HAVE_foo variables
/^HAVE_ATOLL *=/s/@HAVE_ATOLL@/0/
/^HAVE_CHOWN *=/s/@HAVE_CHOWN@/1/
/^HAVE_CLOSEDIR *=/s/@HAVE_CLOSEDIR@/1/
/^HAVE_DECL_GETPAGESIZE *=/s/@HAVE_DECL_GETPAGESIZE@/1/
/^HAVE_DECL_SETENV *=/s/@HAVE_DECL_SETENV@/1/
/^HAVE_DECL_STRDUP *=/s/@HAVE_DECL_STRDUP@/1/
# The following two rely on msdos/inttypes.h redirections
/^HAVE_DECL_STRTOIMAX *=/s/@HAVE_DECL_STRTOIMAX@/1/
/^HAVE_DECL_STRTOUMAX *=/s/@HAVE_DECL_STRTOUMAX@/1/
/^HAVE_DIRENT_H *=/s/@HAVE_DIRENT_H@/1/
/^HAVE_DUP2 *=/s/@HAVE_DUP2@/1/
/^HAVE_FSYNC *=/s/@HAVE_FSYNC@/1/
/^HAVE_FTRUNCATE *=/s/@HAVE_FTRUNCATE@/1/
/^HAVE_GETHOSTNAME *=/s/@HAVE_GETHOSTNAME@/1/
/^HAVE_GETLOGIN *=/s/@HAVE_GETLOGIN@/1/
/^HAVE_GETPAGESIZE *=/s/@HAVE_GETPAGESIZE@/1/
/^HAVE_INTTYPES_H *=/s/@HAVE_INTTYPES_H@/HAVE_INTTYPES_H/
/^HAVE_LINK *=/s/@HAVE_LINK@/1/
/^HAVE_LONG_LONG_INT *=/s/@HAVE_LONG_LONG_INT@/1/
/^HAVE_LSTAT *=/s/@HAVE_LSTAT@/HAVE_LSTAT/
/^HAVE_MAKEINFO *=/s/@HAVE_MAKEINFO@/yes/
/^HAVE_MEMCHR *=/s/@HAVE_MEMCHR@/1/
/^HAVE_MKFIFO *=/s/@HAVE_MKFIFO@/1/
/^HAVE_MKNOD *=/s/@HAVE_MKNOD@/1/
/^HAVE_MKSTEMP *=/s/@HAVE_MKSTEMP@/1/
/^HAVE_OPENDIR *=/s/@HAVE_OPENDIR@/1/
/^HAVE_PCLOSE *=/s/@HAVE_PCLOSE@/1/
/^HAVE_POPEN *=/s/@HAVE_POPEN@/1/
/^HAVE_POSIX_SIGNALBLOCKING *=/s/@HAVE_POSIX_SIGNALBLOCKING@/1/
/^HAVE_RANDOM_H *=/s/@HAVE_RANDOM_H@/1/
/^HAVE_RAISE *=/s/@HAVE_RAISE@/1/
/^HAVE_RANDOM *=/s/@HAVE_RANDOM@/1/
/^HAVE_READDIR *=/s/@HAVE_READDIR@/1/
/^HAVE_REWINDDIR *=/s/@HAVE_REWINDDIR@/1/
/^HAVE_SETENV *=/s/@HAVE_SETENV@/1/
/^HAVE_SIGACTION *=/s/@HAVE_SIGACTION@/1/
/^HAVE_SIGNED_SIG_ATOMIC_T *=/s/@HAVE_SIGNED_SIG_ATOMIC_T@/1/
/^HAVE_SIGNED_WINT_T *=/s/@HAVE_SIGNED_WINT_T@/1/
/^HAVE_SIGSET_T *=/s/@HAVE_SIGSET_T@/1/
/^HAVE_SLEEP *=/s/@HAVE_SLEEP@/1/
/^HAVE_STDINT_H *=/s/@HAVE_STDINT_H@/HAVE_STDINT_H/
/^HAVE_STRPBRK *=/s/@HAVE_STRPBRK@/1/
/^HAVE_STRSEP *=/s/@HAVE_STRSEP@/1/
/^HAVE_STRTOD *=/s/@HAVE_STRTOD@/1/
/^HAVE_STRTOLL *=/s/@HAVE_STRTOLL@/1/
/^HAVE_STRTOULL *=/s/@HAVE_STRTOULL@/1/
/^HAVE_STRUCT_TIMEVAL *=/s/@HAVE_STRUCT_TIMEVAL@/1/
/^HAVE_SYMLINK *=/s/@HAVE_SYMLINK@/1/
/^HAVE_SYS_PARAM_H *=/s/@HAVE_SYS_PARAM_H@/1/
/^HAVE_SYS_TIME_H *=/s/@HAVE_SYS_TIME_H@/1/
/^HAVE_SYS_TYPES_H *=/s/@HAVE_SYS_TYPES_H@/1/
/^HAVE_TYPE_VOLATILE_SIG_ATOMIC_T *=/s/@HAVE_TYPE_VOLATILE_SIG_ATOMIC_T@/1/
/^HAVE_UNISTD_H *=/s/@HAVE_UNISTD_H@/1/
/^HAVE_UNSIGNED_LONG_LONG_INT *=/s/@HAVE_UNSIGNED_LONG_LONG_INT@/1/
/^HAVE_USLEEP *=/s/@HAVE_USLEEP@/1/
/^HAVE_WCHAR_H *=/s/@HAVE_WCHAR_H@/1/
/^HAVE_WCHAR_T *=/s/@HAVE_WCHAR_T@/1/
/^HAVE__BOOL *=/s/@HAVE__BOOL@/1/
/^HAVE__EXIT *=/s/@HAVE__EXIT@/1/
/^HAVE_[^ =]* *= *@/s/@[^@\n]*@/0/
/^INCLUDE_NEXT *=/s/@INCLUDE_NEXT@/include_next/
/^INCLUDE_NEXT_AS_FIRST_DIRECTIVE *=/s/@[^@\n]*@/include_next/
/^LDFLAGS *=/s/@[^@\n]*@//
/^LD_FIRSTFLAG *=/s/@[^@\n]*@//
/^LIB_PTHREAD *=/s/@[^@\n]*@//
/^LIB_PTHREAD_SIGMASK *=/s/@[^@\n]*@//
/^LIBS *=/s/@[^@\n]*@//
/^MAKEINFO *=/s/@MAKEINFO@/makeinfo/
# MKDIR_P lines are edited further below
/^MKDIR_P *=/s/@MKDIR_P@//
/^NEXT_AS_FIRST_DIRECTIVE_DIRENT_H *=/s/@[^@\n]*@/<dirent.h>/
/^NEXT_AS_FIRST_DIRECTIVE_ERRNO_H *=/s/@[^@\n]*@//
/^NEXT_AS_FIRST_DIRECTIVE_FCNTL_H *=/s/@[^@\n]*@/<fcntl.h>/
/^NEXT_AS_FIRST_DIRECTIVE_GETOPT_H *=/s/@[^@\n]*@/<getopt.h>/
/^NEXT_AS_FIRST_DIRECTIVE_SIGNAL_H *=/s/@[^@\n]*@/<signal.h>/
/^NEXT_AS_FIRST_DIRECTIVE_STDARG_H *=/s/@[^@\n]*@//
/^NEXT_AS_FIRST_DIRECTIVE_STDDEF_H *=/s/@[^@\n]*@//
/^NEXT_AS_FIRST_DIRECTIVE_STDINT_H *=/s/@[^@\n]*@/<stdint.h>/
/^NEXT_AS_FIRST_DIRECTIVE_STDIO_H *=/s/@[^@\n]*@/<stdio.h>/
/^NEXT_AS_FIRST_DIRECTIVE_STDLIB_H *=/s/@[^@\n]*@/<stdlib.h>/
/^NEXT_AS_FIRST_DIRECTIVE_STRING_H *=/s/@[^@\n]*@/<string.h>/
/^NEXT_AS_FIRST_DIRECTIVE_SYS_SELECT_H *=/s/@[^@\n]*@//
/^NEXT_AS_FIRST_DIRECTIVE_SYS_STAT_H *=/s!@[^@\n]*@!<sys/stat.h>!
/^NEXT_AS_FIRST_DIRECTIVE_SYS_TIME_H *=/s/@[^@\n]*@//
/^NEXT_AS_FIRST_DIRECTIVE_SYS_TYPES_H *=/s!@[^@\n]*@!<sys/types.h>!
/^NEXT_AS_FIRST_DIRECTIVE_TIME_H *=/s/@[^@\n]*@/<time.h>/
/^NEXT_AS_FIRST_DIRECTIVE_UNISTD_H *=/s/@[^@\n]*@/<unistd.h>/
/^NEXT_DIRENT_H *=/s/@[^@\n]*@/<dirent.h>/
/^NEXT_ERRNO_H *=/s/@[^@\n]*@//
/^NEXT_FCNTL_H *=/s/@[^@\n]*@/<fcntl.h>/
/^NEXT_GETOPT_H *=/s/@[^@\n]*@/<getopt.h>/
/^NEXT_STDARG_H *=/s/@[^@\n]*@//
/^NEXT_SIGNAL_H *=/s/@[^@\n]*@/<signal.h>/
/^NEXT_STDDEF_H *=/s/@[^@\n]*@//
/^NEXT_STDIO_H *=/s/@[^@\n]*@/<stdio.h>/
/^NEXT_STDINT_H *=/s/@[^@\n]*@/<stdint.h>/
/^NEXT_STDLIB_H *=/s/@[^@\n]*@/<stdlib.h>/
/^NEXT_STRING_H *=/s/@[^@\n]*@/<string.h>/
/^NEXT_SYS_SELECT_H *=/s/@[^@\n]*@//
/^NEXT_SYS_STAT_H *=/s!@[^@\n]*@!<sys/stat.h>!
/^NEXT_SYS_TIME_H *=/s/@[^@\n]*@//
/^NEXT_SYS_TYPES_H *=/s!@[^@\n]*@!<sys/types.h>!
/^NEXT_TIME_H *=/s/@[^@\n]*@/<time.h>/
/^NEXT_UNISTD_H *=/s/@[^@\n]*@/<unistd.h>/
/^OBJEXT *=/s/@[^@\n]*@/o/
/^PRAGMA_COLUMNS *=/s/@[^@\n]*@//
/^PRAGMA_SYSTEM_HEADER *=/s/@[^@\n]*@/\\\#pragma GCC system_header/
/^PTHREAD_H_DEFINES_STRUCT_TIMESPEC *=/s/@[^@\n]*@/0/
/^PTRDIFF_T_SUFFIX *=/s/@[^@\n]*@//
/^RANLIB *=/s/@[^@\n]*@/ranlib/
# We never want any gnulib replacement functions
/^REPLACE_[^ =]* *= *@/s/@[^@\n]*@/0/
/^SIG_ATOMIC_T_SUFFIX *=/s/@SIG_ATOMIC_T_SUFFIX@//
/^SIZE_T_SUFFIX *=/s/@SIZE_T_SUFFIX@/u/
/^ALLOCA_H *=/s/@[^@\n]*@/alloca.h/
/^BYTESWAP_H *=/s/@[^@\n]*@/byteswap.h/
/^DIRENT_H *=/s/@[^@\n]*@//
/^ERRNO_H *=/s/@[^@\n]*@//
/^EXECINFO_H *=/s/@[^@\n]*@/execinfo.h/
/^STDBOOL_H *=/s/@[^@\n]*@//
/^STDALIGN_H *=/s/@[^@\n]*@/stdalign.h/
/^STDARG_H *=/s/@[^@\n]*@//
/^STDDEF_H *=/s/@[^@\n]*@//
/^STDINT_H *=/s/@[^@\n]*@/stdint.h/
/^SYS_TIME_H_DEFINES_STRUCT_TIMESPEC *=/s/@[^@\n]*@/0/
/^TIME_H_DEFINES_STRUCT_TIMESPEC *=/s/@[^@\n]*@/0/
/^UNISTD_H_HAVE_WINSOCK2_H *=/s/@[^@\n]*@/0/
/^UNISTD_H_HAVE_WINSOCK2_H_AND_USE_SOCKETS *=/s/@[^@\n]*@/0/
/^UNDEFINE_STRTOK_R *=/s/@UNDEFINE_STRTOK_R@/0/
/^WCHAR_T_SUFFIX *=/s/@WCHAR_T_SUFFIX@/h/
/^WINT_T_SUFFIX *=/s/@WINT_T_SUFFIX@//
/^WINDOWS_64_BIT_OFF_T *=/s/@WINDOWS_64_BIT_OFF_T@/0/
/^WINDOWS_64_BIT_ST_SIZE *=/s/@WINDOWS_64_BIT_ST_SIZE@/0/
# Tailor lists of files to what we need
/am__append_[1-9][0-9]* *=.*gettext\.h/s/@[^@\n]*@/\#/
/am__append_2 *=.*verify\.h/s/@[^@\n]*@//
/^@gl_GNULIB_ENABLED_tempname_TRUE@/s/@[^@\n]*@//
/^gl_LIBOBJS *=/s/@[^@\n]*@/getopt.o getopt1.o memrchr.o sig2str.o time_r.o getloadavg.o pthread_sigmask.o mkostemp.o fpending.o fdatasync.o execinfo.o/
/^am__append_[1-9][0-9]* *=/,/^[^ 	]/{
  s/ *inttypes\.h//
  s| *sys/select\.h||
  s| *sys/time\.h||
}
/^am_libgnu_a_OBJECTS *=/,/^[^ 	]/{
  s/allocator\.\$(OBJEXT) //
  s/binary-io\.\$(OBJEXT) //
  s/careadlinkat\.\$(OBJEXT) //
  s/pipe2\.\$(OBJEXT) //
  s/acl-errno-valid\.\$(OBJEXT) //
  s/file-has-acl\.\$(OBJEXT) //
  s/qcopy-acl\.\$(OBJEXT) //
  s/qset-acl\.\$(OBJEXT) //
  s/openat-die\.\$(OBJEXT) //
  s/save-cwd\.\$(OBJEXT) //
}
/^srcdir *=/s/@[^@\n]*@/./
/^top_srcdir *=/s/@[^@\n]*@/../
/^top_builddir *=/s/@[^@\n]*@/../
s/@PRAGMA_SYSTEM_HEADER@/\#pragma GCC system_header/
s/@PRAGMA_COLUMNS@//
#
# Delete the recipes we don't want to get in our way.
/^\$(srcdir)\/Makefile\.in:/,/^[ 	][ 	]*\$(AUTOMAKE)/d
/^Makefile:/,/^[ 	][ 	]*esac/d
/^\$(top_srcdir)\/configure:/,/^\$(am__aclocal_m4_deps):/d
/^\$(top_builddir)\/config.status:/,/^$/d
#
# Remove the dependencies on $(top_builddir)/config.status
s/ $(top_builddir)\/config.status//
#
# Fix the include-dependency lines
s/^@AMDEP_TRUE@//
s/^@AMDEP_FALSE@/\#/
s/@am__include@/include/
s/@am__quote@//g
s/^@am__fastdepCC_TRUE@//
s/^@am__fastdepCC_FALSE@/\#/
#
# Fix the *-clean rules, to not use a Unixy `test' command.  Empty
# lists are replaced with a dummy file, to avoid an error message from
# "rm -f" with no arguments.
/^CLEANFILES *= *$/c\
CLEANFILES = xyzzy
/^CONFIG_CLEAN_FILES *= *$/c\
CONFIG_CLEAN_FILES = xyzzy
/^CONFIG_CLEAN_VPATH_FILES *= *$/c\
CONFIG_CLEAN_VPATH_FILES = xyzzy
s/^	-*test -z.*|| rm/	 -rm/
s/@echo /@djecho /
#
# Fix the recipes for header files
s/^@GL_GENERATE_ALLOCA_H_TRUE@//
s/^@GL_GENERATE_ALLOCA_H_FALSE@/\#/
s/^@GL_GENERATE_BYTESWAP_H_TRUE@//
s/^@GL_GENERATE_BYTESWAP_H_FALSE@/\#/
s/^@GL_GENERATE_ERRNO_H_TRUE@/\#/
s/^@GL_GENERATE_ERRNO_H_FALSE@//
s/^@GL_GENERATE_EXECINFO_H_TRUE@//
s/^@GL_GENERATE_EXECINFO_H_FALSE@/\#/
s/^@GL_GENERATE_STDBOOL_H_TRUE@/\#/
s/^@GL_GENERATE_STDBOOL_H_FALSE@//
s/^@GL_GENERATE_STDALIGN_H_TRUE@//
s/^@GL_GENERATE_STDALIGN_H_FALSE@/\#/
s/^@GL_GENERATE_STDARG_H_TRUE@/\#/
s/^@GL_GENERATE_STDARG_H_FALSE@/\#/
s/^@GL_GENERATE_STDDEF_H_TRUE@/\#/
s/^@GL_GENERATE_STDDEF_H_FALSE@/\#/
s/^@GL_GENERATE_STDINT_H_TRUE@//
s/^@GL_GENERATE_STDINT_H_FALSE@/\#/
s/^@GL_GENERATE_STDALIGN_H_TRUE@//
s/^@GL_GENERATE_STDALIGN_H_FALSE@/\#/
/^arg-nonnull\.h:/,/^[ 	][ 	]*mv /c\
arg-nonnull.h: $(top_srcdir)/build-aux/snippet/arg-nonnull.h\
	sed -n -e '/GL_ARG_NONNULL/,$$p' < $(top_srcdir)/build-aux/snippet/arg-nonnull.h > $@
/^cxxdefs\.h:/,/^[ 	][ 	]*mv /c\
cxxdefs.h: $(top_srcdir)/build-aux/snippet/cxxdefs.h\
	sed -n -e '/_GL_CXXDEFS/,$$p' < $(top_srcdir)/build-aux/snippet/cxxdefs.h > $@
/^warn-on-use\.h:/,/^[ 	][ 	]*mv /c\
warn-on-use.h: $(top_srcdir)/build-aux/snippet/warn-on-use.h\
	sed -n -e '/^.ifndef/,$$p' < $(top_srcdir)/build-aux/snippet/warn-on-use.h > $@
s/^	[ 	]*{ echo \(.*\); \\/	 djecho \1 > $@-t/
s/^	[ 	]*{ echo \(.*\) && \\/	 djecho \1 > $@-t/
s/^	[ 	]*cat \(.*\); \\/	 sed -e '' \1 >> $@-t/
s/ \&\& \\ *$//
s/\.in-h\;  *\\$/.in-h >> $@-t/
/^	[ 	]*} /d
/^stdio\.h:/,/^	[ 	]*mv /{
  s/'\; \\ *$/' >> $@-t/
}
/^stdlib\.h:/,/^	[ 	]*mv /{
  s/'\; \\ *$/' >> $@-t/
}
/^unistd\.h:/,/^	[ 	]*mv /{
  s/'\; \\ *$/' >> $@-t/
}
/^string\.h:/,/^	[ 	]*mv /{
  s/'\; \\ *$/' >> $@-t/
  /< \$(srcdir)\/string\.in-h >>/d
}
s!\$(MKDIR_P)[ 	][ 	]*sys!command.com /c "if not exist sys\\stat.h md sys"!
/^	@for dir in/,/^[^	 ]/c\
	-rm -rf $(MOSTLYCLEANDIRS)
/^	 *-test . /d
#
# Fix the Automake verbosity stuff
/^	 *\$(AM_V_at)/s/\$(AM_V_at)//
/^	 *\$(AM_V_AR)/s/\$(AM_V_AR)//
/^	 *\$(AM_V_CC)/s/\$(AM_V_CC)//
